apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def VERSION = '0.0.1-ALPHA'

Properties properties = new Properties()
def propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    properties.load(propertiesFile.newDataInputStream())
}
ext.USER = properties.getProperty('USER')
ext.API_KEY = properties.getProperty('API_KEY')

android {

    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/crawlspace-release.aar")
            groupId 'supahsoftware'
            artifactId 'crawlspace'
            version VERSION
        }
    }
}

bintray {
    user = USER
    key = API_KEY
    publications = ['Production']
    configurations = ['archives']
    dryRun = false
    override = false
    publish = true
    pkg {
        repo = 'CrawlSpace'
        name = 'supahsoftware.crawlspace'
        websiteUrl = 'https://github.com/SupahSoftware/CrawlSpace'
        issueTrackerUrl = 'https://github.com/SupahSoftware/CrawlSpace/issues'
        vcsUrl = 'https://github.com/SupahSoftware/CrawlSpace.git'

        licenses = ['MIT']
        labels = ['Android', 'Library', 'Room']
        publicDownloadNumbers = true

        version {
            name = VERSION
            vcsTag = VERSION
            released = new Date()
        }
    }
}
